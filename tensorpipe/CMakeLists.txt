# Copyright (c) Facebook, Inc. and its affiliates.
# All rights reserved.
#
# This source code is licensed under the BSD-style license found in the
# LICENSE file in the root directory of this source tree.

add_library(tensorpipe
  channel/error.cc
  channel/helpers.cc
  channel/registry.cc
  common/address.cc
  common/error.cc
  common/system.cc
  core/context.cc
  core/error.cc
  core/listener.cc
  core/pipe.cc
  proto/core.proto
  transport/connection.cc
  transport/error.cc
  transport/registry.cc)

# Support `#include <tensorpipe/foo.h>`.
target_include_directories(tensorpipe PUBLIC $<BUILD_INTERFACE:${PROJECT_SOURCE_DIR}>)

# Support `#include <tensorpipe/proto/foo.pb.h>`.
target_include_directories(tensorpipe PRIVATE ${PROJECT_BINARY_DIR})

# Support `#include "proto/foo.pb.h"`, as generated by protoc.
target_include_directories(tensorpipe PRIVATE ${PROJECT_BINARY_DIR}/tensorpipe)


## Channels

### basic

target_sources(tensorpipe PRIVATE
  channel/basic/channel.cc
  channel/basic/context.cc)

### xth

target_sources(tensorpipe PRIVATE
  channel/xth/channel.cc
  channel/xth/context.cc
  proto/channel/xth.proto)

### cma

if(TP_ENABLE_CMA)
  target_sources(tensorpipe PRIVATE
    channel/cma/channel.cc
    channel/cma/context.cc
    proto/channel/cma.proto)
  target_compile_definitions(tensorpipe PUBLIC TP_ENABLE_CMA)
endif()

### mpt

target_sources(tensorpipe PRIVATE
  channel/mpt/channel.cc
  channel/mpt/context.cc
  proto/channel/mpt.proto)


## Transports

### uv

target_sources(tensorpipe PRIVATE
  transport/uv/connection.cc
  transport/uv/context.cc
  transport/uv/error.cc
  transport/uv/listener.cc
  transport/uv/loop.cc
  transport/uv/sockaddr.cc
  transport/uv/uv.cc)
find_package(uv REQUIRED)
target_link_libraries(tensorpipe PRIVATE uv::uv)

### shm

if(TP_ENABLE_SHM)
  target_sources(tensorpipe PRIVATE
    transport/shm/context.cc
    transport/shm/connection.cc
    transport/shm/fd.cc
    transport/shm/listener.cc
    transport/shm/loop.cc
    transport/shm/reactor.cc
    transport/shm/socket.cc
    util/ringbuffer/shm.cc
    util/shm/segment.cc)
  target_compile_definitions(tensorpipe PUBLIC TP_ENABLE_SHM)
endif()


if(APPLE)
  find_library(CF CoreFoundation)
  find_library(IOKIT IOKit)
  target_link_libraries(tensorpipe PRIVATE ${CF} ${IOKIT})
endif()


## Protobufs

if(NOT TARGET protobuf::libprotobuf)
  if(Protobuf_DIR)
    find_package(Protobuf 3 REQUIRED HINTS "${Protobuf_DIR}")
  else()
    find_package(Protobuf 3 REQUIRED)
  endif()
endif()

target_link_libraries(tensorpipe PRIVATE protobuf::libprotobuf)
protobuf_generate(TARGET tensorpipe)


## Python bindings

if(TP_BUILD_PYTHON)
  add_subdirectory(python)
endif()


## Benchmarks

add_subdirectory(benchmark)


## Tests

if(BUILD_TESTING)
  add_subdirectory(test)
endif()


## Install

# NOTE: We only support install for the dynamic library, as installing a static
# library requires exporting its dependencies, which is not done in libuv.
if(BUILD_SHARED_LIBS)
  include(GNUInstallDirs)
  install(TARGETS tensorpipe
    EXPORT tensorpipe-targets
    LIBRARY DESTINATION ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR}
    ARCHIVE DESTINATION ${CMAKE_INSTALL_PREFIX}/${CMAKE_INSTALL_LIBDIR})

  install(EXPORT tensorpipe-targets
          DESTINATION share/cmake/tensorpipe
          FILE TensorpipeTargets.cmake)
endif()
